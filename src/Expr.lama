-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of 
    Assn (v, expr) -> case evalList(c, {v, expr}) of 
      [[s, w], Ref (x) : v : _] -> [ [s <- [x, v], w], v ]
    esac
  | Seq (expr1, expr2) -> case eval(c, expr1) of
      [c, _] -> eval(c, expr2)
    esac
  | Skip -> [c, T]
  | Read (x) -> case readWorld(w) of rw@[value, new_w] -> [[ s <- [x, value], new_w ], T] esac
  | Write (expr) -> case eval(c, expr) of 
      [c, v] -> [[s, writeWorld(v, w)], v]
    esac
  | If (expr, expr1, expr2) -> case eval(c, expr) of 
      [c, v] -> if (v) then eval(c, expr1) else eval(c, expr2) fi
    esac
  | While (expr1, expr2) -> case eval(c, expr1) of 
      [c, v] -> if (v) then eval(eval(c, expr2)[0], expr) else [c, T] fi
    esac
  | DoWhile (expr1, expr2) -> eval(eval(c, expr1)[0], While (expr2, expr1))
  | Var (x) -> [c, s(x)]
  | Ref (x) -> [c, Ref (x)]
  | Const (v) -> [c, v]
  | Binop (op, expr1, expr2) -> case evalList(c, {expr1, expr2}) of
      [c, val1 : val2 : _] -> [c, evalOp(op, val1, val2)]
    esac
  | Ignore (expr) -> case eval(c, expr) of
      [c, _] -> [c, T]
    esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
