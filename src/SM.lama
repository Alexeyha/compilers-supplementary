-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> "READ"
  | WRITE        -> "WRITE"
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | LDA   (x)    -> sprintf ("LDA %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | STI          -> "STI"
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  | DUP          -> "DUP"
  | DROP         -> "DROP"  
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

fun evalInsn(env, c@[stack, state, world], ins) {
  case ins of
    READ       -> case readWorld(world) of 
                     [value, w] -> [value : stack, state, w]
                  esac

  | WRITE      -> [tl(stack), state, writeWorld(hd(stack), world)]

  | BINOP (op) -> case stack of 
                   (eval2:eval1:rest) -> [evalOp(op, eval1, eval2):rest, state, world]
                  esac

  | LD    (x)  -> [state(x) : stack, state, world]

  | ST    (x)  -> [tl(stack), state <- [x, hd(stack)], world]

  | CONST (n)  -> [n : stack, state, world]

  | LABEL (lab) -> c

  | JMP   (lab) -> [c, fromLabel(env, lab)]

  | CJMP  (c, lab) -> case stack of 
      head:rest ->
        if ((compare(c, "nz") == 0 && head != 0) !! (compare(c, "z") == 0 && head == 0)) then 
          [[rest, state, world], fromLabel(env, lab)] 
        else 
          [rest, state, world]
        fi
    esac
  | STI -> case stack of 
      (Ref(x):v:rest) -> [rest, state <- [x, v], world]
    esac
  | DUP -> [hd(stack):stack, state, world]
  | LDA (x) -> [Ref(x):stack, state, world]
  | DROP -> [tl(stack), state, world]
  esac
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
  case insns of
    {} -> c 
  | insn:rest -> case evalInsn(env, c, insn) of 
      [conf, next] -> eval(env, conf, next)
    | conf -> eval(env, conf, rest)
    esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }
  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip              -> [false, env, emptyBuffer ()]
    | Var    (x)        -> [false, env, singletonBuffer (LD (x))]
    | Ref    (x)        -> [false, env, singletonBuffer (LDA (x))]
    | Const  (n)        -> [false, env, singletonBuffer (CONST (n))]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | While (expr, s) -> 
        case env.genLabels(3) of
          [exprLab, postExprLab, stmtLab, env] -> case compile(postExprLab, env, expr) of
            [postExprLabUsed, env, exprCode] -> case compile(exprLab, env, s) of 
              [_, env, stCode] -> [false, env, singletonBuffer(JMP (exprLab)) <+ LABEL (stmtLab) <+> stCode <+ LABEL (exprLab) <+> exprCode <+> label(postExprLab, postExprLabUsed) <+ CJMP ("nz", stmtLab) ]
            esac
          esac
        esac
    | If (e, s1, s2) -> case env.genLabels(2) of 
        [exprLab, elseLab, env] -> case compile(exprLab, env, e) of 
            [exprLabUsed, env, exprCode] -> case compile(lab, env, s1) of
              [_, env, s1Code] -> case compile(lab, env, s2) of
                [_, env, s2Code] -> [true, env, exprCode <+> label(exprLab, exprLabUsed) <+ CJMP ("z", elseLab) <+> s1Code <+ JMP (lab) <+ LABEL (elseLab) <+> s2Code]
              esac 
            esac 
          esac 
        esac
    | DoWhile (s, expr) -> case env.genLabels(3) of 
        [exprLab, postExprLab, stmtLab, env] -> case compile(postExprLab, env, expr) of
          [postExprLabUsed, env, exprCode] -> case compile(exprLab, env, s) of 
            [exprLabUsed, env, sCode] -> [false, env, singletonBuffer(LABEL (stmtLab)) <+> sCode <+> label(exprLab, exprLabUsed) <+> exprCode <+> label(postExprLab, postExprLabUsed) <+ CJMP ("nz", stmtLab)]
          esac
        esac
      esac
    | Assn (v, expr) -> case env.genLabels(2) of 
        [vLab, exprLab, env] -> case compile(vLab, env, v) of 
            [vLabUsed, env, vCode] -> case compile(exprLab, env, expr) of
              [exprLabUsed, env, exprCode] -> [false, env, exprCode <+> label(exprLab, exprLabUsed) <+ DUP <+> vCode <+> label(vLab, vLabUsed) <+ STI]
          esac
        esac
      esac
    | Read (x) -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (expr) -> case genLabel(env) of 
        [exprLab, env] -> case compile(exprLab, env, expr) of 
          [exprLabUsed, env, exprCode] -> [false, env, exprCode <+> label(exprLab, exprLabUsed) <+ WRITE]
        esac
      esac
    | Binop (op, expr1, expr2) -> case env.genLabels(2) of 
        [expr1Lab, expr2Lab, env] -> case compile(expr1Lab, env, expr1) of 
          [expr1LabUsed, env, expr1Code] -> case compile(expr2Lab, env, expr2) of
            [expr2LabUsed, env, expr2Code] -> [false, env, expr1Code <+> label(expr1Lab, expr1LabUsed) <+> expr2Code <+> label(expr2Lab, expr2LabUsed) <+ BINOP (op)]
          esac
        esac
      esac
    | Ignore (expr) -> case genLabel(env) of 
        [exprLab, env] -> case compile(exprLab, env, expr) of 
          [exprLabUsed, env, exprCode] -> [false, env, exprCode <+> label(exprLab, exprLabUsed) <+ DROP]
        esac
      esac
    | _                 ->  failure ("compileSM not implemented\n")
    esac
  }

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $ code <+> label (endLab, endLabUsed) 
       esac
  esac
}
