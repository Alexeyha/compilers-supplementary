-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun evalScope(c@[s, w], defs, expr) {
  fun addDefsToState(s, defs) {
    foldr(fun (s, def) {
        case def of 
          Var (vars) -> addNames(s, vars)
        | Fun (name, args, body) -> addFunction(s, name, args, body)
        esac
      }, s, defs)
  }

  case defs of 
    {} -> eval(c, expr)
  | _ -> case eval([addDefsToState(enterScope(s), defs), w], expr) of [c@[s, w], v] -> [[leaveScope(s), w], v] esac
  esac
}

fun evalCall(c@[s, w], name, args) {
  fun addArgsToState(s, args) {
    foldr(fun (s, [name, value]) {
      addName(s, name, value)
    }, s, args)
  }

  case lookup(s, name) of 
    Fun (vars, body) -> case evalList(c, args) of 
      [c@[args_s, w], vals] -> case eval([addArgsToState(enterFunction(args_s), zip(vars, vals)), w], body) of 
        [c@[body_s, w], v] -> [[leaveFunction(args_s, getGlobal(body_s)), w], v] 
      esac
    esac
  esac
}

(* Assignment *)
fun eval (c@[s, w], expr) {
  case expr of 
    Assn (v, expr) -> case evalList(c, {v, expr}) of 
      [[s, w], Ref (x) : v : _] -> [ [s <- [x, v], w], v ]
    esac
  | Seq (expr1, expr2) -> case eval(c, expr1) of
      [c, _] -> eval(c, expr2)
    esac
  | Skip -> [c, T]
  | Read (x) -> case readWorld(w) of rw@[value, new_w] -> [[ s <- [x, value], new_w ], T] esac
  | Write (expr) -> case eval(c, expr) of 
      [c, v] -> [[s, writeWorld(v, w)], T]
    esac
  | If (expr, expr1, expr2) -> case eval(c, expr) of 
      [c, v] -> if (v) then eval(c, expr1) else eval(c, expr2) fi
    esac
  | While (expr1, expr2) -> case eval(c, expr1) of 
      [c, v] -> if (v) then eval(eval(c, expr2)[0], expr) else [c, T] fi
    esac
  | DoWhile (expr1, expr2) -> eval(eval(c, expr1)[0], While (expr2, expr1))
  | Var (x) -> [c, lookup(s, x)]
  | Ref (x) -> [c, Ref (x)]
  | Const (v) -> [c, v]
  | Binop (op, expr1, expr2) -> case evalList(c, {expr1, expr2}) of
      [c, val1 : val2 : _] -> [c, evalOp(op, val1, val2)]
    esac
  | Ignore (expr) -> case eval(c, expr) of
      [c, _] -> [c, T]
    esac
  | Scope (defs, expr) -> evalScope(c, defs, expr)
  | Call (name, args) -> evalCall(c, name, args)
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}